(ns anime.production-test
  (:require [clojure.test :refer [deftest is]]
            [anime.production :as production]))

(deftest twelve-parallel-stages
  (is (= 12 (count production/stages)))
  (is (= 12 (count production/default-stage-status)))
  (is (every? #(= "pending" %) (vals production/default-stage-status))))

(deftest cut-priority-derivation
  (is (= "normal" (production/derive-cut-priority {})))
  (is (= "normal" (production/derive-cut-priority {"script" "approved" "layout" "pending"})))
  (is (= "retake" (production/derive-cut-priority {"script" "approved" "layout" "retake"})))
  (is (= "approved" (production/derive-cut-priority
                     (into {} (map (fn [s] [s "approved"])) production/stages)))))

(deftest layer-specs-are-consistent
  (is (= 8 (count production/layer-specs)))
  (let [stage-set (set production/stages)]
    (doseq [[k {:keys [ns prefix stage id-key uri-key]}] production/layer-specs]
      (is (string? ns) (str k " has a record namespace"))
      (is (string? prefix))
      (is (contains? stage-set stage) (str k " advances a known stage"))
      (is (keyword? id-key))
      (is (keyword? uri-key)))))

(deftest schema-declares-unique-ids-for-hierarchy-and-layers
  (doseq [attr [:work/id :episode/id :scene/id :cut/id :retake/id]]
    (is (= :db.unique/identity (get-in production/schema [attr :db/unique]))))
  (doseq [{:keys [ns]} (vals production/layer-specs)]
    (let [attr (keyword ns "id")]
      (when (contains? production/schema attr)
        (is (= :db.unique/identity (get-in production/schema [attr :db/unique])))))))

(deftest identity-helpers
  (is (= "ai.gftd.apps.animeka.cut" (production/collection "ai.gftd.apps.animeka" "cut")))
  (is (= "at://did:web:example.com/app.example.cut/cut-ep01-003"
         (production/vertex-uri "did:web:example.com" "app.example" "cut" "cut-ep01-003")))
  (is (= "cut-ep01-003"
         (production/rkey-from-id "at://did:web:example.com/app.example.cut/cut-ep01-003")))
  (is (= "cut-ep01-003" (production/rkey-from-id "cut-ep01-003")))
  (is (= "spring-storm-2" (production/slug "Spring Storm 2"))))
